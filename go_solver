#!/bin/csh

# script to run the solver

# read DATA/Par_file to get information about the run

# name of the file that contains the list of machines
set machine_file = "mymachines"

set my_local_path = `grep LOCAL_PATH DATA/Par_file | cut -d '=' -f 2 `

# compute total number of processors needed
set NCHUNKS = `grep NCHUNKS DATA/Par_file | cut -d '=' -f 2 `
set NPROC_XI = `grep NPROC_XI DATA/Par_file | cut -d '=' -f 2 `
set NPROC_ETA = `grep NPROC_ETA DATA/Par_file | cut -d '=' -f 2 `

# total number of processors is the product of the values read
@ numprocessors = $NCHUNKS * $NPROC_XI * $NPROC_ETA

# suppress old processor list files
rm -f PI*

if ( -f $machine_file ) then
  echo " "
  echo using machine file \"$machine_file\"
  echo " "
  echo list of active machines:
  echo " "
  cat $machine_file
  echo " "
endif

echo NCHUNKS = $NCHUNKS
echo NPROC_XI = $NPROC_XI
echo NPROC_ETA = $NPROC_ETA
echo " "
echo starting MPI solver on $numprocessors processors
echo " "
echo starting run in current directory $PWD
echo " "
echo mesh files will be read from directory $my_local_path
echo " "

#### use this on Beowulf
mpirun -nolocal -machinefile $machine_file -np $numprocessors $PWD/xspecfem3D > OUTPUT_FILES/output_xspecfem3D.txt &

#### use this on SGI
#  mpirun -np $numprocessors xspecfem3D

#### use this on Compaq Dec Alpha
# dmpirun -np $numprocessors xspecfem3D

